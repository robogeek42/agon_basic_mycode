10 REM Stars - vertical scroll
11 REM Assif Mirza
20 MODE 8 : REM 320x240, 40x30, 64 colours
25 SCRW% = 320 : SCRH% = 240
30 DIM SPOS%(100,2)
40 FOR S% = 0 TO 100 
50 SPOS%(S%, 0) = RND(SCRW%) - 1
60 SPOS%(S%, 1) = RND(SCRH%) - 1
70 SPOS%(S%, 2) = RND(3)
80 NEXT S%
100 REPEAT
110 PROCclear
120 FOR S% = 0 TO 100 
130 LET SPOS%(S%,1) = SPOS%(S%,1) - SPOS%(S%,2)
135 IF SPOS%(S%,1) < 0 THEN PROCnewstar(S%)
140 NEXT
150 PROCshow
190 UNTIL FALSE

200 DEF PROCshow
210 FOR S% = 0 TO 100 
220 PROCsetcols(S%)
250 PLOT 69, SPOS%(S%, 0)*4, SPOS%(S%, 1)*4
260 NEXT S%
270 ENDPROC

300 DEF PROCclear
310 GCOL 0,0
320 FOR S% = 0 TO 100 
350 PLOT 69, SPOS%(S%, 0)*4, SPOS%(S%, 1)*4
360 NEXT S%
370 ENDPROC

400 DEF PROCsetcols(N%)
410 IF SPOS%(N%,2) = 1 THEN GCOL 0, 8
420 IF SPOS%(N%,2) = 2 THEN GCOL 0, 7
430 IF SPOS%(N%,2) = 3 THEN GCOL 0, 15
440 ENDPROC

500 DEF PROCnewstar(NS%)
510 SPOS%(NS%,1) = SCRH% - 1
520 SPOS%(NS%,0) = RND(SCRW%) - 1
530 SPOS%(NS%,2) = RND(3)
590 ENDPROC
